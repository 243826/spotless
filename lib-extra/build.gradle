plugins {
	id 'java-library'
	id 'dev.equo.p2deps'
}
ext.artifactId = project.artifactIdLibExtra
version = rootProject.spotlessChangelog.versionNext
apply from: rootProject.file('gradle/java-setup.gradle')
apply from: rootProject.file('gradle/java-publish.gradle')

dependencies {
	api project(':lib')
	// misc useful utilities
	implementation "com.diffplug.durian:durian-core:${VER_DURIAN}"
	implementation "com.diffplug.durian:durian-collect:${VER_DURIAN}"
	// needed by GitAttributesLineEndings
	implementation "org.eclipse.jgit:org.eclipse.jgit:${VER_JGIT}"
	implementation "com.googlecode.concurrent-trees:concurrent-trees:2.6.1"
	// for eclipse
	implementation "dev.equo.ide:solstice:0.17.0"

	// testing
	testImplementation project(':testlib')
	testImplementation "org.junit.jupiter:junit-jupiter:${VER_JUNIT}"
	testImplementation "org.assertj:assertj-core:${VER_ASSERTJ}"
	testImplementation "com.diffplug.durian:durian-testlib:${VER_DURIAN}"
}

apply from: rootProject.file('gradle/special-tests.gradle')
tasks.withType(Test).configureEach {
	if (JavaVersion.current().isCompatibleWith(JavaVersion.VERSION_16)) {
		// needed for EclipseCdtFormatterStepTest
		jvmArgs '--add-opens=java.base/java.lang=ALL-UNNAMED'
	}
}

def NEEDS_P2_DEPS = [
	'jdt'
]
for (needsP2 in NEEDS_P2_DEPS) {
	sourceSets.register(needsP2) {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
		java {}
	}
}
jar {
	for (needsP2 in NEEDS_P2_DEPS) {
		from sourceSets.getByName(needsP2).output.classesDirs
	}
}
tasks.withType(Test).configureEach {
	dependsOn jar
	doFirst {
		classpath += jar.outputs.files
	}
}

apply plugin: 'dev.equo.p2deps'
p2deps {
	into 'jdtCompileOnly', {
		p2repo 'https://download.eclipse.org/eclipse/updates/4.26/'
		install 'org.eclipse.jdt.core'
		addFilter 'minimize', { filter ->
			filter.exclude 'org.eclipse.ant.core'
			filter.exclude 'org.eclipse.core.expressions'
			filter.exclude 'org.eclipse.core.filesystem'
		}
	}
}

// we'll hold the core lib to a high standard
spotbugs { reportLevel = 'low' } // low|medium|high (low = sensitive to even minor mistakes)

