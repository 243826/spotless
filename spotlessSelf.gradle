// applied by SelfTest
plugins {
	id 'com.diffplug.gradle.spotless'
	id 'java'
}

repositories { jcenter() }
spotless {
	java {
		target fileTree('.') {
			include '**/*.java'
			exclude '_ext/*/build/**'
		}
		custom 'noInternalDeps', {
			if (it.contains('import org.gradle.internal.')) {
				throw new AssertionError("Accidental internal import")
			}
		}
		bumpThisNumberIfACustomStepChanges(1)

		licenseHeaderFile 'spotless.license'
		importOrderFile   'spotless.importorder'
		eclipseFormatFile 'spotless.eclipseformat.xml'
		trimTrailingWhitespace()
	}
	groovy {
		target fileTree('.') {
			include '**/*.gradle'
			exclude '_ext/**'
		}
		custom 'preventFormatPingPong', { return it.replaceAll('}[ \t\f]+}', '}}') }
		greclipseFormat().configFile('spotless.eclipseformat.xml', 'spotless.groovyformat.prefs')
	}
	freshmark {
		target '**/*.md'
		propertiesFile('gradle.properties')
		properties {
			it.put('yes', ':+1:')
			it.put('no', ':white_large_square:')
		}
	}
	format 'misc', {
		target '**/*.md', '**/*.gitignore'
		indentWithTabs()
		trimTrailingWhitespace()
		endWithNewline()
	}
}
