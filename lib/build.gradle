plugins {
	id 'java-library'
}
ext.artifactId = project.artifactIdLib
version = rootProject.spotlessChangelog.versionNext
apply from: rootProject.file('gradle/java-setup.gradle')
apply from: rootProject.file('gradle/java-publish.gradle')

sourceSets {
	sortPom {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output

		java {
		}
	}
}

dependencies {
	// zero runtime reqs is a hard requirements for spotless-lib
	// if you need a dep, put it in lib-extra
	testImplementation "org.junit.jupiter:junit-jupiter:${VER_JUNIT}"
	testImplementation "org.assertj:assertj-core:${VER_ASSERTJ}"
	testImplementation "com.diffplug.durian:durian-testlib:${VER_DURIAN}"

	// used for pom sorting
	sortPomCompileOnly 'com.github.ekryd.sortpom:sortpom-sorter:3.0.0'
}

// we'll hold the core lib to a high standard
spotbugs { reportLevel = 'low' } // low|medium|high (low = sensitive to even minor mistakes)

test { useJUnitPlatform() }

jar {
	// Add directories with generated sources (might be several - one for every supported language).
	from sourceSets.sortPom.output.classesDirs
	// Add output directories of the sourceset's resources.
	from sourceSets.sortPom.output.resourcesDir
}
